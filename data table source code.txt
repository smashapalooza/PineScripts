// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© Amphibiantrading

//@version=5
indicator("Data Table", overlay = true)

//inputs
var g1 = 'Moving Averages'
ma1len = input.int(13, 'Moving Average 1', inline = '1', group = g1)
ma2len = input.int(21, 'Moving Average 2', inline = '2', group = g1)
ma3len = input.int(50, 'Moving Average 3', inline = '3', group = g1)
ma4len = input.int(200, 'Moving Average 4', inline = '4', group = g1)
ma1Type = input.string('EMA', ' ', options = ['EMA', 'SMA'], inline = '1', group = g1)
ma2Type = input.string('EMA', ' ', options = ['EMA', 'SMA'], inline = '2', group = g1)
ma3Type = input.string('EMA', ' ', options = ['EMA', 'SMA'], inline = '3', group = g1)
ma4Type = input.string('EMA', ' ', options = ['EMA', 'SMA'], inline = '4', group = g1)
var g2 = 'Lookbacks'
atrLen = input.int(23, 'ADR Length', inline = '5', group = g2)
ppLen = input.int(23, 'P.P. Lookback', inline = '5', group = g2)
udLen = input.int(23, 'Up Down Vol Lookback', inline = '6', group = g2)
var g3 = 'Table Options'
tableBG = input.color(color.white, 'Table Background Color', inline = '7', group = g3)
i_tableypos = input.string("Middle", " ", options = ["Top", "Middle", "Bottom"], inline = "8", group = g3)
i_tablexpos = input.string("Right", " ", options = ["Right","Center", "Left"], inline = "8", group = g3)
tableTextSize = input.string("Normal", "Table Size", options = ["Small","Normal","Large","Huge"], inline = '9', group = g3)

//mas
ma1 = ma1Type == 'EMA' ? ta.ema(close, ma1len) : ta.sma(close, ma1len)
ma2 = ma2Type == 'EMA' ? ta.ema(close, ma2len) : ta.sma(close, ma2len)
ma3 = ma3Type == 'EMA' ? ta.ema(close, ma3len) : ta.sma(close, ma3len)
ma4 = ma4Type == 'EMA' ? ta.ema(close, ma4len) : ta.sma(close, ma4len)
atr = ta.atr(atrLen)

//requests 
weeklyHigh = request.security(syminfo.tickerid, 'W', high)
weeklyLow = request.security(syminfo.tickerid, 'W', low)
yearHigh = request.security(syminfo.tickerid, 'W', ta.highest(high, 52))
yearLow = request.security(syminfo.tickerid, 'W', ta.lowest(low, 52))

//variables
var float h10 = 0.0
var table dataTable = table.new(str.lower(i_tableypos) + '_' + str.lower(i_tablexpos), 2, 17, bgcolor = tableBG, frame_color = color.black, frame_width = 2, border_color = color.black, border_width = 2)
var float ath = 0.0
var float atl = 9900000

//arrays
vols = array.new<float>()
var pps = array.new<int>()

//calculations
distMa13 = ((close / ma1) -1 ) * 100
distMa21 = ((close / ma2) -1 ) * 100
distMa50 = ((close / ma3) -1 ) * 100
distMa200 = ((close / ma4) -1 ) *100
distB1321 = ((ma1 / ma2) -1 ) * 100
distB2150 = ((ma2 / ma3) -1 ) * 100
distB1350 = ((ma1 / ma3) -1 ) * 100
adr = (atr / close) * 100
closeRange = (close - low) / (high - low) *100
weeklyCR = (close - weeklyLow) / (weeklyHigh - weeklyLow) * 100
distYearHigh = ((close - yearHigh) / yearHigh) * 100
distYearLow = ((close - yearLow) / yearLow) * 100
ath := high > ath ? high : ath
atl := low < atl ? low : atl
distATH = ((ath - close ) / close) * 100
distATL = ((close - atl) / atl) * 100
upVol = close > close[1] ? volume : 0
dnVol = close < close[1] ? volume : 0
sumUp = math.sum(upVol, udLen)
sumDn = math.sum(dnVol, udLen)
upDnVolRatio = sumUp / sumDn

// pocket pivots
for i = 0 to 10
    if close[i] < close[i + 1]
        array.push(vols, volume[i])
        h10 := array.max(vols)

pocketpivot = close > close[1] and volume > h10

if pocketpivot
    pps.push(bar_index)

for [ind, day] in pps
    if bar_index >= day + (ppLen - 1)
        pps.remove(ind)


if barstate.islast
    table.cell(dataTable, 0, 0, 'Dist. ' + str.tostring(ma1Type) + ' ' + str.tostring(ma1len) + ':', text_halign = text.align_left, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 0, 1, 'Dist. ' + str.tostring(ma2Type) + ' ' + str.tostring(ma2len) + ':', text_halign = text.align_left, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 0, 2, 'Dist. ' + str.tostring(ma3Type) + ' ' + str.tostring(ma3len) + ':', text_halign = text.align_left, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 0, 3, 'Dist. ' + str.tostring(ma4Type) + ' ' + str.tostring(ma4len) + ':', text_halign = text.align_left, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 0, 4, str.tostring(ma1Type) + ' ' + str.tostring(ma1len) + '/' + str.tostring(ma2len) + ':', text_halign = text.align_left, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 0, 5, str.tostring(ma2Type) + ' ' + str.tostring(ma2len) + '/' + str.tostring(ma3len) + ':', text_halign = text.align_left, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 0, 6, str.tostring(ma1Type) + ' ' + str.tostring(ma1len) + '/' + str.tostring(ma3len) + ':', text_halign = text.align_left, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 0, 7, 'U/D Ratio: ', text_halign = text.align_left, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 0, 8, 'PP Count: ', text_halign = text.align_left, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 0, 9, 'ADR: ', text_halign = text.align_left, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 0, 10, 'DCR: ', text_halign = text.align_left, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 0, 11, 'WCR: ', text_halign = text.align_left, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 0, 12, 'Dist. 52WH: ', text_halign = text.align_left, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 0, 13, 'Dist. 52WL: ', text_halign = text.align_left, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 0, 14, 'Dist. ATH: ', text_halign = text.align_left, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 0, 15, 'Dist. ATL: ', text_halign = text.align_left, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 1, 0, str.tostring(distMa13, '#.##') + '%', text_color = (distMa13 > 0 ? color.green : color.red), text_size = str.lower(tableTextSize))
    table.cell(dataTable, 1, 1, str.tostring(distMa21, '#.##') + '%', text_color = (distMa13 > 0 ? color.green : color.red), text_size = str.lower(tableTextSize))
    table.cell(dataTable, 1, 2, str.tostring(distMa50, '#.##') + '%', text_color = (distMa13 > 0 ? color.green : color.red), text_size = str.lower(tableTextSize))
    table.cell(dataTable, 1, 3, str.tostring(distMa200, '#.##') + '%', text_color = (distMa13 > 0 ? color.green : color.red), text_size = str.lower(tableTextSize))
    table.cell(dataTable, 1, 4, str.tostring(distB1321, '#.##') + '%', text_color = color.blue, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 1, 5, str.tostring(distB2150, '#.##') + '%', text_color = color.blue, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 1, 6, str.tostring(distB1350, '#.##') + '%', text_color = color.blue, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 1, 7, str.tostring(upDnVolRatio, '#.##') + '%', text_color = (upDnVolRatio > 2.5 ? color.green : color.red), text_size = str.lower(tableTextSize))
    table.cell(dataTable, 1, 8, str.tostring(pps.size()), text_color = (pps.size() > 0 ? color.green : color.red), text_size = str.lower(tableTextSize))
    table.cell(dataTable, 1, 9, str.tostring(adr, '#.##') + '%', text_color = color.blue, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 1, 10, str.tostring(closeRange, '#') + '%', text_color = (closeRange >= 50 ? color.green : color.red), text_size = str.lower(tableTextSize))
    table.cell(dataTable, 1, 11, str.tostring(weeklyCR, '#') + '%', text_color = (closeRange >= 50 ? color.green : color.red), text_size = str.lower(tableTextSize))
    table.cell(dataTable, 1, 12, str.tostring(distYearHigh, '#') + '%', text_color = (distYearHigh <= 25 ? color.green : color.red), text_size = str.lower(tableTextSize))
    table.cell(dataTable, 1, 13, str.tostring(distYearLow, '#') + '%', text_color = (distYearLow >= 50 ? color.green : color.red), text_size = str.lower(tableTextSize))
    table.cell(dataTable, 1, 14, str.tostring(distATH, '#') + '%', text_color = color.blue, text_size = str.lower(tableTextSize))
    table.cell(dataTable, 1, 15, str.tostring(distATL, '#') + '%', text_color = color.blue, text_size = str.lower(tableTextSize))
    
